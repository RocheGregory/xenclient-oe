SRC_URI[md5sum] = "3a2b23f29013605f721ebdfc29de9c92"
SRC_URI[sha256sum] = "cdf99f9add677a925ee87a5b87e94eb595ed9b72034453c195ef9379bd26552a"
DESCRIPTION = "ghc-native"
LICENSE = "Glasgow"
LIC_FILES_CHKSUM = "file://LICENSE;md5=7cb08deb79c4385547f57d6bb2864e0f"

PR = "r2"

FILESEXTRAPATHS_prepend := "${THISDIR}/ghc-${PV}:"

SRC_URI = "http://www.haskell.org/ghc/dist/6.12.1/ghc-6.12.1-src.tar.bz2 \
	file://bfd-error.patch \
	file://ghc6-fix-linking-with-newer-binutils.patch \
	file://fix-linker.patch \
	"

S = "${WORKDIR}/ghc-${PV}"

PARALLEL_MAKE=""

inherit native
# huge hack to be able to generate ipks from native package
python() {
    bb.data.delVarFlag("do_package", "noexec", d);
    bb.data.delVarFlag("do_package_write_ipk", "noexec", d);
    d.setVarFlag('do_configure', 'umask', 022)
    d.setVarFlag('do_compile', 'umask', 022)
    d.appendVarFlag('do_install', 'depends', ' virtual/fakeroot-native:do_populate_sysroot')
    d.setVarFlag('do_install', 'fakeroot', 1)
    d.setVarFlag('do_install', 'umask', 022)
    d.appendVarFlag('do_package', 'depends', ' virtual/fakeroot-native:do_populate_sysroot')
    d.setVarFlag('do_package', 'fakeroot', 1)
    d.setVarFlag('do_package', 'umask', 022)
    d.setVarFlag('do_package_setscene', 'fakeroot', 1)

}

RDEPENDS_${PN} =+ "ghc-runtime-native"

do_configure() {
	cp `which pwd` utils/ghc-pwd/ghc-pwd
	export CPP=`which cpp`
	./configure --prefix=${STAGING_DIR} --bindir ${STAGING_BINDIR} --libdir ${STAGING_LIBDIR} --datadir ${STAGING_DATADIR} --enable-shared

	# This goes here so we do not depend nor require libgmp installed on target
	echo "INTEGER_LIBRARY = integer-simple" > mk/build.mk
	# Various flags to trim down ghc build time
	echo "HADDOCK_DOCS = NO" >> mk/build.mk
	echo "BUILD_DOCBOOK_HTML = NO" >> mk/build.mk
	echo "BUILD_DOCBOOK_PS = NO" >> mk/build.mk
	echo "BUILD_DOCBOOK_PDF = NO" >> mk/build.mk
	echo "SplitObjs = NO" >> mk/build.mk
	echo "SRC_HC_OPTS = -H64m -O0 -fasm" >> mk/build.mk
	echo "GhcStage1HcOpts = -O -fasm" >> mk/build.mk
	echo "GhcStage2HcOpts = -O0 -fasm" >> mk/build.mk
	echo "GhcLibHcOpts    = -O -fasm" >> mk/build.mk
	# set this to "v p" if profiling libraries are necessary
	echo "GhcLibWays = v dyn" >> mk/build.mk

    # look there for bfd.h stupid cow:
    echo "STANDARD_OPTS += \"-I${STAGING_INCDIR_NATIVE}\"" >> rts/ghc.mk
}

do_compile() {
	make
}

#do_stage() {
#	make install
#}

do_install() {
    make install "DESTDIR=${D}"
}
